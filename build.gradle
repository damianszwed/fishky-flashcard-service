buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'checkstyle'
    id 'net.ltgt.apt' version '0.21'
    id 'com.github.spotbugs' version '4.7.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.springframework.boot' version '2.5.2'
    id 'info.solidsoft.pitest' version '1.7.4'
    id 'com.palantir.docker' version '0.34.0'
}

ext {
    //TODO(Damian.Szwed) Should I use set()?
    junitPlatformVersion = '1.8.2'
    junitJupiterVersion = '5.8.2'
    mockitoVersion = '3.3.3'
    sleuthVersion = '2.0.1.RELEASE'
    zipkinVersion = '2.0.1.RELEASE'
}

group = 'com.github.damianszwed'

jar {
    //TODO(Damian.Szwed) Is below line needed at all?
    archiveBaseName.set('fishky-flashcard-service')
    enabled = false
    archiveClassifier.set('')
}

checkstyle {
    toolVersion = '10.3.1'
}

test {
    useJUnitPlatform()
}

spotbugs {
    toolVersion = '4.0.2'
    ignoreFailures = false
    effort = "max"
    reportLevel = "high"
    omitVisitors = ["FindNonShortCircuit"]
    excludeFilter = file("$rootProject.projectDir/config/spotbugs/excludeFilter.xml")
}

docker {
    name "${jar.archiveBaseName.get()}:${version}"
    files jar.archiveFile
    buildArgs(['JAR_FILE': "${jar.archiveFileName.get()}"])
}

pitest {
    junit5PluginVersion = '0.16'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.11
targetCompatibility = 1.11

dependencies {
    implementation "org.springframework.boot:spring-boot-configuration-processor"
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'
    implementation 'org.springframework.security:spring-security-config'
    implementation 'org.springframework.security:spring-security-oauth2-jose'
    implementation 'org.springframework.security:spring-security-oauth2-resource-server'

    implementation 'io.vavr:vavr:0.10.3'
    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation "org.junit.platform:junit-platform-engine:${junitPlatformVersion}"
    testImplementation "org.junit.platform:junit-platform-runner:${junitPlatformVersion}"
    testImplementation "org.junit.platform:junit-platform-commons:${junitPlatformVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testCompileOnly 'org.projectlombok:lombok:1.18.12'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
}

task updateVersion {
    doLast {
        ant.replaceregexp(match: 'image: damianiero/fishky-flashcard-service:(.*)', replace: "image: damianiero/fishky-flashcard-service:${version}", flags: 'g', byline: true) {
            fileset(dir: '.', includes: 'kubernetes-deployment.yaml')
        }
    }
}

task kubernetesDeployment(type: Exec) {
    commandLine 'kubectl', 'apply', '-f', 'kubernetes-deployment.yaml'
}

task kubernetesService(type: Exec) {
    commandLine 'kubectl', 'apply', '-f', 'kubernetes-service.yaml'
}

task tagDockerImage(type: Exec) {
    commandLine 'docker', 'tag', "fishky-flashcard-service:${version}", "damianiero/fishky-flashcard-service:${version}"
}

task pushDockerImage(type: Exec) {
    dependsOn tagDockerImage
    commandLine 'docker', 'push', "damianiero/fishky-flashcard-service:${version}"
}

task deployByKubernetes {
    dependsOn kubernetesService, kubernetesDeployment, pushDockerImage, 'docker', build, updateVersion
    build.mustRunAfter updateVersion
}

task deployByLocalKubernetes {
    dependsOn kubernetesService, kubernetesDeployment, 'docker', build, updateVersion
    build.mustRunAfter updateVersion
}

task deleteKubernetesDeployment(type: Exec) {
    commandLine 'kubectl', 'delete', '-f', 'kubernetes-deployment.yaml'
}

task deleteKubernetesService(type: Exec) {
    commandLine 'kubectl', 'delete', '-f', 'kubernetes-service.yaml'
}

task undeploy {
    dependsOn deleteKubernetesDeployment, deleteKubernetesService
}